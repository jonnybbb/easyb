/**
 * DbUnit easyb plug-in build file
 */
includeTool << gant.tools.Ivy

version = "0.7a"
defaultdir = "target"
libdir = "${defaultdir}/lib"
jarincludes = "**/*.jar"
classdir = "${defaultdir}/classes"
gsourcedir = "src/groovy"
gbehavedir = "behavior/groovy"

target(clean:"cleans target dir"){
	Ant.delete(dir:defaultdir)
}

Ant.path(id:"build.classpath"){
	fileset(dir:libdir, includes:jarincludes)
}

target(init:"init dirs"){
	Ant.mkdir(dir:classdir)
	Ant.mkdir(dir:libdir)
	getdeps()
}

target(configuregroovy:""){
 depends(init)
 Ant.taskdef(name:"groovyc",
    classname:"org.codehaus.groovy.ant.Groovyc", classpathref:"build.classpath"){
 }
}

target(getdeps:"retrieves dependencies"){
  Ivy.configure(file:"./ivyconfig.xml")
  Ivy.retrieve(pattern:"./target/lib/[artifact]-[revision].[ext]")
}


target("default":"Default task is make a release."){
   	release()
}


target("groovyc":"compile Groovy classes"){
	depends(configuregroovy)
	genericgroovyc(gsourcedir, classdir)
}

target(jar:"archive project"){
   depends(groovyc)
   Ant.jar(destfile:"${defaultdir}/easyb-dbunit-plugin-${version}.jar", basedir:classdir) {
     metainf(dir:"./cfg/web/META-INF", includes:"**")
   }
}

/**
 * note for releases, this plug-in relies on the main
 * easyb binary, so there is no need to include
 * groovy, cli, and easyb itself. Also,
 * hsqldb isn't needed as it's used for verification.
 */
target(release:"compile and archive"){
  depends(jar)
  depends("validate-plugin")
  Ant.mkdir(dir:"${defaultdir}/release")
  Ant.move(file:"${defaultdir}/easyb-dbunit-plugin-${version}.jar", todir:"${defaultdir}/release")
  Ant.move(todir:"${defaultdir}/release"){
  	fileset(dir:"./target/lib/"){
  		exclude(name:"**/ant-*.jar")
  		exclude(name:"**/hsqldb-*.jar")
  		exclude(name:"**/easyb-*.jar")
  		exclude(name:"**/groovy-all-*.jar")
  		exclude(name:"**/commons-cli-*.jar")
  	}
  }
}

target(dist:"create a distribution from the release"){
	depends(release)
	

	def tarfilename = "${defaultdir}/dist/easyb-dbunit-plugin-${version}.tar"

	Ant.mkdir(dir:"${defaultdir}/dist")
	Ant.tar(tarfile:tarfilename, basedir:"${defaultdir}/release")
    Ant.gzip(zipfile:"${tarfilename}.gz", src:tarfilename)
    Ant.delete(file:tarfilename)
}

genericgroovyc = { source, destination ->
    Ant.groovyc(srcdir:source, destdir:destination){
	      classpath(){
	      	fileset(dir:libdir){
	      		include(name:jarincludes)
	      	}
	      	dirset(dir:classdir)
	     }
	}
}

target(compilebehaviors:"compile all behavior classes"){
	depends(groovyc)
	genericgroovyc(gbehavedir, classdir)
}


target("validate-plugin":"runs the easyb ant task"){
  	depends(jar)

	Ant.taskdef(name:"easyb", classname:"org.disco.easyb.ant.SpecificationRunnerTask"){
	    
		classpath(){
        	pathelement(location:"${defaultdir}/lib/easyb-${version}.jar")
    	}
	}

	Ant.easyb(failureProperty:"easyb.failed"){
	
    	classpath() {
        	fileset(dir:libdir, includes:jarincludes)
        	pathelement(location:"${defaultdir}/easyb-dbunit-plugin-${version}.jar")
        	pathelement(location:gbehavedir)
      	}
      	

		report(location:"${defaultdir}/behavior-report.xml", format:"xmlbehavior")
	    report(location:"${defaultdir}/story-report.txt", format:"txtstory")

    	behaviors(dir:gbehavedir){
	        include(name:"**/*story.groovy")
	  
		}
	}

  	Ant.fail(if:"easyb.failed", message:"Execution halted as behaviors failed")
}
